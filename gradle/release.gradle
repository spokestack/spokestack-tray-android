apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'net.researchgate.release'

dokkaJavadoc {
    outputDirectory.set(file("$buildDir/javadoc"))
    dokkaSourceSets {
        named("main") {
            jdkVersion.set(8)
            includeNonPublic.set(false)
            skipDeprecated.set(false)
            reportUndocumented.set(true)

            // Do not create index pages for empty packages
            skipEmptyPackages.set(true)
            sourceLink {
                localDirectory.set(file("SpokestackTray/src/main/java"))
                remoteUrl.set(
                        URL("https://github.com/spokestack/spokestack-tray-android/blob/master/SpokestackTray/src/main/java"))
                remoteLineSuffix.set("#L")
            }
        }
    }
}

task androidJavadocsJar(type: Jar, dependsOn: dokkaJavadoc) {
    getArchiveClassifier().set('javadoc')
    from dokkaJavadoc.outputDirectory
}

task androidSourcesJar(type: Jar) {
    getArchiveClassifier().set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            tray(MavenPublication) {
                from components.release

                artifact androidSourcesJar
                artifact androidJavadocsJar

                groupId = 'io.spokestack'
                artifactId = 'tray'

                pom {
                    name = 'Spokestack Tray'
                    description = 'A UI widget for integrating with Spokestack'
                    url = 'https://github.com/spokestack/spokestack-tray-android'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'joshz'
                            name = 'Josh Ziegler'
                            email = 'josh@spokestack.io'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/spokestack/spokestack-tray-android.git'
                        developerConnection = 'scm:git:git@github.com:spokestack/spokestack-tray-android.git'
                        url = 'https://github.com/spokestack/spokestack-tray-android'
                    }
                }
            }
        }

        signing {
            sign publishing.publications.tray
        }
    }
}

release {
    git {
        requireBranch = '/v?[0-9]+.[0-9]+.[0-9]+/'
    }
}

afterReleaseBuild.dependsOn(
        ':SpokestackTray:publishToSonatype',
        ':closeAndReleaseSonatypeStagingRepository'
)
